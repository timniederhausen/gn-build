# Copyright (c) 2013 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/libc++/settings.gni")
import("//build/toolchain/apple/symbols.gni")
import("//build/toolchain/mac/mac_sdk.gni")
import("//build/toolchain/sysroot.gni")

# This is included by reference in the //build/config/compiler config that
# is applied to all targets. It is here to separate out the logic.
config("compiler") {
  # These flags are shared between the C compiler and linker.
  common_mac_flags = []

  # CPU architecture.
  if (current_cpu == "x64") {
    clang_arch = "x86_64"
  } else if (current_cpu == "x86") {
    clang_arch = "i386"
  } else if (current_cpu == "armv7" || current_cpu == "arm") {
    clang_arch = "armv7"
  } else if (current_cpu == "arm64") {
    clang_arch = current_cpu
  } else {
    assert(false, "unknown current_cpu $current_cpu")
  }
  if (host_os == "mac") {
    common_mac_flags += [
      "-arch",
      clang_arch,
    ]
  } else {
    common_mac_flags += [ "--target=$clang_arch-apple-macos" ]
  }

  # This is here so that all files get recompiled after an Xcode update.
  # (defines are passed via the command line, and build system rebuild things
  # when their commandline changes). Nothing should ever read this define.
  if (mac_use_sdk) {
    defines = [ "CR_XCODE_VERSION=$xcode_version" ]
  }

  asmflags = common_mac_flags
  cflags = common_mac_flags

  # Without this, the constructors and destructors of a C++ object inside
  # an Objective C struct won't be called, which is very bad.
  cflags_objcc = [ "-fobjc-call-cxx-cdtors" ]

  ldflags = common_mac_flags

  if (save_unstripped_output) {
    ldflags += [ "-Wcrl,unstripped," + rebase_path(root_out_dir) ]
  }

  if (export_libcxxabi_from_executables) {
    ldflags += [ "-Wl,-undefined,dynamic_lookup" ]
  }
}

# This is included by reference in the //build/config/compiler:runtime_library
# config that is applied to all targets. It is here to separate out the logic
# that is Mac-only. Please see that target for advice on what should go in
# :runtime_library vs. :compiler.
config("runtime_library") {
  if (mac_use_sdk) {
    common_flags = [
      "-isysroot",
      rebase_path(sysroot, root_build_dir),
      "-mmacosx-version-min=$mac_deployment_target",
    ]
  } else {
    common_flags=[]
  }

  asmflags = common_flags
  cflags = common_flags
  ldflags = common_flags

  # Prevent Mac OS X AssertMacros.h (included by system header) from defining
  # macros that collide with common names, like 'check', 'require', and
  # 'verify'.
  # http://opensource.apple.com/source/CarbonHeaders/CarbonHeaders-18.1/AssertMacros.h
  defines = [ "__ASSERT_MACROS_DEFINE_VERSIONS_WITHOUT_UNDERSCORES=0" ]
}

# On Mac, this is used for everything except static libraries.
config("mac_dynamic_flags") {
  ldflags = [ "-Wl,-ObjC" ]  # Always load Objective-C categories and classes.
}

# On Mac, this is used only for executables.
config("mac_executable_flags") {
  # Remove this when targeting >=10.7 since it is the default in that config.
  ldflags = [ "-Wl,-pie" ]  # Position independent.
}

# The ldflags referenced below are handled by
# //build/toolchain/apple/linker_driver.py.
# Remove this config if a target wishes to change the arguments passed to the
# strip command during linking. This config by default strips all symbols
# from a binary, but some targets may wish to specify an exports file to
# preserve specific symbols.
config("strip_all") {
  if (enable_stripping) {
    ldflags = [ "-Wcrl,strip,-x,-S" ]
  }
}
