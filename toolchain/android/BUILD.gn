# Copyright 2013 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/toolchain/android/settings.gni")
import("//build/toolchain/toolchain.gni")
import("//build/toolchain/sysroot.gni")
import("//build/toolchain/gcc_toolchain.gni")
import("//build/toolchain/clang.gni")

# The Android GCC toolchains share most of the same parameters, so we have this
# wrapper around gcc_toolchain to avoid duplication of logic.
#
# Parameters:
#  - toolchain_root
#      Path to cpu-specific toolchain within the ndk.
#  - sysroot
#      Sysroot for this architecture.
#  - lib_dir
#      Subdirectory inside of sysroot where libs go.
#  - binary_prefix
#      Prefix of compiler executables.
template("android_gcc_toolchain") {
  gcc_toolchain(target_name) {
    assert(defined(invoker.toolchain_args),
           "toolchain_args must be defined for android_gcc_toolchain()")
    toolchain_args = invoker.toolchain_args
    toolchain_args.current_os = "android"

    # Make our manually injected libs relative to the build dir.
    _ndk_lib =
        rebase_path(invoker.sysroot + "/" + invoker.lib_dir + "/" + android_sdk_version, root_build_dir)

    libs_section_prefix = "$_ndk_lib/crtbegin_dynamic.o"
    libs_section_postfix = "$_ndk_lib/crtend_android.o"

    solink_libs_section_prefix = "$_ndk_lib/crtbegin_so.o"
    solink_libs_section_postfix = "$_ndk_lib/crtend_so.o"

    _android_tool_prefix = "${invoker.toolchain_root}/bin/"

    # The tools should be run relative to the build dir.
    _tool_prefix = rebase_path("$_android_tool_prefix", root_build_dir)

    # Use the clang specified by the toolchain if there is one. Otherwise fall
    # back to the global flag.
    if (defined(toolchain_args.is_clang)) {
      toolchain_uses_clang = toolchain_args.is_clang
    } else {
      toolchain_uses_clang = is_clang
    }

    assert(toolchain_uses_clang, "We support clang toolchain only")

    #_prefix = rebase_path("$clang_base_path/bin/", root_build_dir)
    _prefix = _tool_prefix
    cc = _prefix + "clang"
    cxx = _prefix + "clang++"
    ar = _tool_prefix + "llvm-ar"
    ld = cc
    readelf = _tool_prefix + "llvm-readelf"
    nm = _tool_prefix + "llvm-nm"
    strip = _tool_prefix + "llvm-strip"

    # Don't use .cr.so for loadable_modules since they are always loaded via
    # absolute path.
    loadable_module_extension = ".so"
  }
}

template("android_gcc_toolchains_helper") {
  android_gcc_toolchain("android_clang_$target_name") {
    forward_variables_from(invoker, "*")
    if (clang_base_path != "") {
      toolchain_root = clang_base_path
    }
    toolchain_args.is_clang = true
  }
}

android_gcc_toolchains_helper("x86") {
  toolchain_root = android_toolchain_root
  lib_dir = "usr/lib/i686-linux-android"
  toolchain_args = {
    current_cpu = "x86"
  }
}

android_gcc_toolchains_helper("arm") {
  toolchain_root = android_toolchain_root
  lib_dir = "usr/lib/i686-linux-android"
  toolchain_args = {
    current_cpu = "arm"
  }
}

android_gcc_toolchains_helper("mipsel") {
  toolchain_root = android_toolchain_root
  lib_dir = "usr/lib/mipsel-linux-android"
  toolchain_args = {
    current_cpu = "mipsel"
  }
}

android_gcc_toolchains_helper("x64") {
  toolchain_root = android_toolchain_root
  lib_dir = "usr/lib64/x86_64-linux-android"
  toolchain_args = {
    current_cpu = "x64"
  }
}

android_gcc_toolchains_helper("arm64") {
  toolchain_root = android_toolchain_root
  lib_dir = "usr/lib/aarch64-linux-android"
  toolchain_args = {
    current_cpu = "arm64"
  }
}

android_gcc_toolchains_helper("mips64el") {
  toolchain_root = android_toolchain_root
  lib_dir = "usr/lib64/mips64el-linux-android"
  toolchain_args = {
    current_cpu = "mips64el"
  }
}
